; J Bolton

; Week 9
; Program 2 - Input int n from the command line, create an array of Fibonacci numbers up to n, and print the array to the screen.

; class heirarchy
.class public FibonacciArray
.super java/lang/Object

; standard initializer
.method public <init>()V
  aload_0
  invokespecial    java/lang/Object/<init>()V
  return
.end method

.method private static fib(I)I
  .limit stack 3
  ; equivalent java code: return(n<2)?n:fib(n-1)+fib(n-2)

  ; base case
  iload_0
  iconst_2
  if_icmplt Base

  ; fib(n-1)
  iload_0
  iconst_1
  isub
  invokestatic FibonacciArray/fib(I)I

  ; fib(n-2)
  iload_0
  iconst_2
  isub
  invokestatic FibonacciArray/fib(I)I

  ; fib(n-1) + fib(n-2)
  iadd
  ireturn

Base:
  iload_0
  ireturn
.end method

.method public static main([Ljava.lang.String;)V
  .limit stack 4
  .limit locals 3

  ; retrieve first argument (n) from command line
  aload_0
  iconst_0
  aaload
  invokestatic java/lang/Integer.parseInt(Ljava/lang/String;)I
  dup

  ; store n as a countdown
  istore_1

  ; create array of length n
  newarray int
  astore_2
  
  ; fill array using a loop
Loop:
  iinc 1 -1 ; n--
  
  ; fill array[n] with fib(n)
  aload_2
  iload_1
  iload_1
  invokestatic FibonacciArray/fib(I)I
  iastore
  
  ; print array[n]
  aload_2
  iload_1
  iaload
  getstatic java/lang/System/out Ljava/io/PrintStream;
  swap
  invokevirtual	java/io/PrintStream/println(I)V
  
  iload_1
  ifgt Loop ; (n>0)

  return

.end method
